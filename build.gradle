plugins {
	id 'java'
	alias(libs.plugins.springBoot)
    alias(libs.plugins.springDependencyMgmt)
    alias(libs.plugins.spotless)
}

group = 'com.paymentchain.business'
version = '0.0.1-SNAPSHOT'
description = 'Billing Microservice'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
    implementation(libs.springBootStarterDataJpa)
    implementation(libs.springBootStarterWeb)
    compileOnly(libs.lombok)
    runtimeOnly(libs.h2)
    annotationProcessor(libs.lombok)
    testImplementation(libs.springBootStarterTest)
    testRuntimeOnly(libs.junitPlatformLauncher)
}

spotless {
    java {
        googleJavaFormat(libs.versions.googleJavaFormat.get())
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register("installGitHooks", Copy) {
    group = "git"
    description = "Copies custom git hooks from `.githooks` to `.git/hooks`"
    from '.githooks'
    into '.git/hooks'
    filePermissions {
        unix('rwxr-xr-x') //Sets file mode to 755
    }

}

